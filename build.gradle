plugins {
    id 'java'
    id 'idea'
    id "io.spring.dependency-management" version "1.0.1.RELEASE"
    id 'maven-publish'
    id 'signing'
    id 'nebula.optional-base' version '3.0.3'
    id 'nebula.provided-base' version '3.0.3'
}

ext {
    pomFile = file("${project.buildDir}/generated-pom.xml")
    isReleaseVersion = !(project.version =~ /-SNAPSHOT$/)
}

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'

        vcs = 'Git'
    }
}

tasks.withType(JavaCompile) {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencyManagement {
    imports {
        mavenBom 'io.spring.platform:platform-bom:Brussels-SR1'
    }
}

dependencies {
    compile("org.springframework.boot:spring-boot-autoconfigure")
    compile("org.springframework.boot:spring-boot-starter-logging")
    compile("org.springframework:spring-webmvc")
    compile('org.springframework:spring-aop')
    compile('org.aspectj:aspectjweaver')

    compile 'org.springframework.retry:spring-retry', optional

    provided 'org.projectlombok:lombok'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'net.javacrumbs.future-converter:future-converter-spring-java8:1.1.0'

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("com.fasterxml.jackson.datatype:jackson-datatype-jdk8")
    testCompile("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml")
    testCompile('org.assertj:assertj-core:3.6.2')
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    required { signatory != null && project.ext.isReleaseVersion }
    sign configurations.archives
}

publishing {
    repositories {
        maven {
            if (project.ext.isReleaseVersion) {
                url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            } else {
                url 'https://oss.sonatype.org/content/repositories/snapshots/'
            }

            credentials {
                username = hasProperty('sonatypeUsername') ? sonatypeUsername : System.getenv('sonatypeUsername')
                password = hasProperty('sonatypePassword') ? sonatypePassword : System.getenv('sonatypePassword')
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }

            pom.withXml {
                // fix maven-publish issue when all maven dependencies are placed into runtime scope
                asNode().dependencies.dependency.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.dependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each { it.scope*.value = 'compile' }

                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name "${project.name}"
                    description "${project.description}"
                    url 'https://github.com/polysantiago/spring-boot-rest-client'
                    inceptionYear 2017
                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://www.opensource.org/licenses/mit-license.php'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'polysantiago'
                            name 'Pablo Santiago'
                            email 'pablo.santiago@gmail.com'
                            url 'https://github.com/polysantiago'
                        }
                    }
                    scm {
                        url 'https://github.com/polysantiago/spring-boot-rest-client'
                        connection 'scm:git:https://github.com/polysantiago/spring-boot-rest-client.git'
                        developerConnection 'scm:git:ssh://git@github.com:polysantiago/spring-boot-rest-client.git'
                    }
                }
            }

            // Sign the pom.xml and artifacts.
            if (signing.required) {
                // Sign the pom.xml.
                pom.withXml {
                    writeTo(project.ext.pomFile)
                    def pomAscFile = signing.sign(project.ext.pomFile).signatureFiles[0]
                    artifact(pomAscFile) {
                        classifier = null
                        extension = 'pom.asc'
                    }
                    project.ext.pomFile.delete()
                }

                // Sign the artifacts.
                project.tasks.signArchives.signatureFiles.each {
                    artifact(it) {
                        def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                        if (matcher.find()) {
                            classifier = matcher.group(1)
                        } else {
                            classifier = null
                        }
                        extension = 'jar.asc'
                    }
                }
            }
        }
    }
}

publish.dependsOn check

model {
    tasks.publishMavenPublicationToMavenLocal {
        dependsOn(project.tasks.signArchives)
    }
    tasks.publishMavenPublicationToMavenRepository {
        dependsOn(project.tasks.signArchives)
    }
}

task install(dependsOn: publishToMavenLocal)

test {
    testLogging {
        exceptionFormat = 'full'
    }
}